apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

ext.versionMajor = 7
ext.versionMinor = 0
ext.versionPatch = 8
ext.postfix = ''

android {
    compileSdkVersion 31
    buildToolsVersion '32.0.0'

    defaultConfig {
        applicationId 'ua.napps.scorekeeper'
        minSdkVersion 24
        targetSdkVersion 31
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode generateVersionCode()
        versionName generateVersionName()
        resConfigs 'en', 'ru', 'uk', 'fr', 'de', 'es', 'ro', 'pl', 'pt'

        manifestPlaceholders += [
                crashlyticsEnabled: false
        ]
    }

    signingConfigs {
        release {
            storeFile new File(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders += [crashlyticsEnabled: true]
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation 'androidx.room:room-runtime:2.4.2'
    implementation "androidx.room:room-rxjava2:2.4.2"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    //noinspection GradleDependency
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.android.billingclient:billing:4.1.0'
    implementation 'com.github.naz013:ColorSlider:2.0.7'
    implementation 'io.github.bitvale:switcher:1.1.2'


    annotationProcessor 'androidx.lifecycle:lifecycle-common-java8:2.4.1'
    annotationProcessor 'androidx.room:room-compiler:2.4.2'
}

def generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

def generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}${ext.postfix}"
}
