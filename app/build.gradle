apply plugin: 'com.android.application'

ext.versionMajor = 3
ext.versionMinor = 0
ext.versionPatch = 0

def appVersionCode = generateVersionCode()

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId 'ua.napps.scorekeeper'
        minSdkVersion 19
        targetSdkVersion 25
        vectorDrawables.useSupportLibrary = true
        versionCode appVersionCode
    }

    signingConfigs {
        release {
            storeFile new File(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            // Uses new built-in shrinker http://tools.android.com/tech-docs/new-build-system/built-in-shrinker
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".dev"
            versionNameSuffix ".dev"
        }
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

ext {
    // App dependencies
    supportLibraryVersion = '26.1.0'
    archLifecycleVersion = "1.0.0-alpha9-1"
    archRoomVersion = "1.0.0-alpha9-1"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:support-vector-drawable:$supportLibraryVersion"
    compile "android.arch.lifecycle:extensions:$archLifecycleVersion"
    compile "android.arch.persistence.room:runtime:$archRoomVersion"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.4'
    compile 'com.google.android:flexbox:0.3.0'
    compile 'com.dmitrymalkovich.android:material-design-dimens:1.4'
    compile 'com.afollestad.material-dialogs:core:0.9.4.7'
    compile 'com.afollestad.material-dialogs:commons:0.9.4.7'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.jakewharton.timber:timber:4.5.1'

    annotationProcessor "android.arch.lifecycle:compiler:$archLifecycleVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$archRoomVersion"
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "$supportLibraryVersion"
            }
        }
    }
}

def generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

task _printVersion << {
    println "\n==================================================================================="
    println "VERSION CODE: " + generateVersionCode()
    println "===================================================================================\n"
}
