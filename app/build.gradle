def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

ext.versionMajor = 8
ext.versionMinor = 0
ext.versionPatch = 5
ext.postfix = ''

android {
    compileSdk 34

    defaultConfig {
        applicationId 'ua.napps.scorekeeper'
        minSdkVersion 26
        targetSdkVersion 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode generateVersionCode()
        versionName generateVersionName()

        manifestPlaceholders += [
                crashlyticsEnabled: false
        ]
    }
    androidResources {
        generateLocaleConfig true
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    signingConfigs {
        release {
            keyAlias localProperties['KEY_ALIAS']
            keyPassword localProperties['KEY_PASSWORD']
            storeFile localProperties['STORE_FILE'] ? file(localProperties['STORE_FILE']) : null
            storePassword localProperties['STORE_PASSWORD']
        }
    }

    buildTypes {
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders += [crashlyticsEnabled: true]
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    namespace 'ua.napps.scorekeeper'
}

dependencies {
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation 'androidx.room:room-runtime:2.6.1'
    implementation "androidx.room:room-rxjava2:2.6.1"
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    //noinspection GradleDependency
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.google.firebase:firebase-crashlytics:19.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.android.billingclient:billing:7.1.1'
    implementation 'com.github.naz013:ColorSlider:2.0.7'
    implementation 'io.github.bitvale:switcher:1.1.2'
    implementation 'com.github.Kunzisoft:AndroidClearChroma:2.6'

    annotationProcessor 'androidx.lifecycle:lifecycle-common-java8:2.8.7'
    annotationProcessor 'androidx.room:room-compiler:2.6.1'
}

def generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

def generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}${ext.postfix}"
}
