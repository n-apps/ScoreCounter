apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

ext.versionMajor = 6
ext.versionMinor = 6
ext.versionPatch = 4
ext.postfix = ''

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'

    defaultConfig {
        applicationId 'ua.napps.scorekeeper'
        minSdkVersion 21
        targetSdkVersion 31
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode generateVersionCode()
        versionName generateVersionName()
        resConfigs "en", "ru", "uk", "fr", "de", "es", "ro", "pl"

        manifestPlaceholders += [
                crashlyticsEnabled: false
        ]
    }

    signingConfigs {
        release {
            storeFile new File(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders += [crashlyticsEnabled: true]
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.play:core:1.10.2'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.room:room-runtime:2.3.0'
    implementation "androidx.room:room-rxjava2:2.3.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    //noinspection GradleDependency
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.android.billingclient:billing:4.0.0'
    implementation 'com.github.naz013:ColorSlider:2.0.7'
    implementation 'petrov.kristiyan:colorpicker-library:1.1.10'

    annotationProcessor 'androidx.lifecycle:lifecycle-common-java8:2.3.1'
    annotationProcessor 'androidx.room:room-compiler:2.3.0'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "$supportLibraryVersion"
            }
        }
    }
}

def generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

def generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}${ext.postfix}"
}
